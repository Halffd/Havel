let fRunning = false
let fTimer = 0
let spaceTimer = 0
let genshinAutomationActive = false

!d => hotkey.toggleOverlay()

!del => hotkey.toggleAutomationTask("autoclicker", "left")

^. => {
  audio.increaseActiveAppVolume(0.05)
  gui.notify("App Volume", audio.getActiveAppVolume() * 100)
}

^, => {
  audio.decreaseActiveAppVolume(0.05)
  gui.notify("App Volume", audio.getActiveAppVolume() * 100)
}

Enter if contains(window.getTitle(), "Chatterino") => print("Enter pressed in chatterino")

h if contains(window.getTitle(), "Genshin Impact") => {
  if io.getKeyState("lctrl") {
    if fTimer != 0 { timer.stop(fTimer); fTimer = 0 }
    fRunning = false
    return null
  }

  if fRunning {
    if fTimer != 0 { timer.stop(fTimer); fTimer = 0 }
    fRunning = false
  }

  fRunning = true
  fTimer = timer.setInterval(100, () => {
    if contains(window.getTitle(), "Genshin Impact") == false {
      timer.stop(fTimer)
      fTimer = 0
      fRunning = false
      return null
    }
    if io.getKeyState("lctrl") {
      timer.stop(fTimer)
      fTimer = 0
      fRunning = false
      return null
    }
    send("f")
  })

  timer.setTimeout(15000, () => {
    if fRunning && fTimer != 0 {
      timer.stop(fTimer)
      fTimer = 0
      fRunning = false
    }
  })
}

~space if contains(window.getTitle(), "Genshin Impact") => {
  if spaceTimer != 0 { return }
  send("{space:up}")
  sleep(100)
  if io.getKeyState("space") == false { return }
  io.disableHotkey("~space")

  spaceTimer = timer.setInterval(100, () => {
    if contains(window.getTitle(), "Genshin Impact") == false {
      timer.stop(spaceTimer)
      spaceTimer = 0
      io.enableHotkey("~space")
      return null
    }
    send("{space}")
  })
}

~space:up if contains(window.getTitle(), "Genshin Impact") => {
  if spaceTimer != 0 { timer.stop(spaceTimer); spaceTimer = 0 }
  send("{space:up}")
  io.enableHotkey("~space")
}

enter if contains(window.getTitle(), "Genshin Impact") => {
  genshinAutomationActive = genshinAutomationActive ? false : true
  if genshinAutomationActive {
    gui.notify("Genshin Automation", "Automation enabled")
  } else {
    gui.notify("Genshin Automation", "Automation disabled")
  }
}

+s if contains(window.getTitle(), "Genshin Impact") => {
  timer.setTimeout(100, () => {
    send("{enter}")
    sleep(100)
    send("f")
  })
}

!+g => {
  if genshinAutomationActive {
    genshinAutomationActive = false
    gui.notify("Genshin Automation", "Automation sequence stopped")
  } else {
    gui.notify("Genshin Automation", "No active automation to stop")
  }
}

^!Up => window.moveToCorners(1)
^!Down => window.moveToCorners(2)
^!Left => window.moveToCorners(3)
^!Right => window.moveToCorners(4)

+!Up => window.resizeToCorner(1)
!+Down => window.resizeToCorner(2)
!+Left => window.resizeToCorner(3)
!+Right => window.resizeToCorner(4)

!a => window.toggleAlwaysOnTop()

!delete => hotkey.toggleAutomationTask("autoclicker", "left")

#l => runDetached("xdg-screensaver lock")

+!Esc => runDetached("gnome-system-monitor &")

#!d => hotkey.showBlackOverlay()

#2 => hotkey.printActiveWindowInfo()

!+i => hotkey.toggleWindowFocusTracking()

^!d => debug.toggleVerboseConditionLogging()
